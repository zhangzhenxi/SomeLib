apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

android {
	compileSdkVersion 24
	buildToolsVersion "24.0.2"
	
	defaultConfig {
		minSdkVersion 15
		targetSdkVersion 22
		versionCode 1
		versionName "1.0"
	}
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])
	compile 'com.android.support:appcompat-v7:24.2.0'
	compile 'com.google.code.gson:gson:2.7'
}

def libVersion = '1.0'
def libGroupId = 'pub.fury.somelib'
def libArtifactId = project.getName()
publishing {
	publications {
		aar(MavenPublication) {
			groupId libGroupId
			version libVersion
			artifactId libArtifactId
			artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")

			pom.withXml {
				def dependencies = asNode().appendNode('dependencies')
				configurations.getByName("_releaseCompile").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
					def dependency = dependencies.appendNode('dependency')
					dependency.appendNode('groupId', it.moduleGroup)
					dependency.appendNode('artifactId', it.moduleName)
					dependency.appendNode('version', it.moduleVersion)
				}
			}
		}
	}
}

artifactory {
	contextUrl = 'http://localhost:8081/artifactory'
	publish {
		repository {
			repoKey = 'libs-release-local'
			username = ARTIFACT_USERNAME
			password = ARTIFACT_USERPWD
		}
		defaults {
			publications('aar')
			publishArtifacts = true
			properties = ['qa.level': 'basic', 'dev.team': 'core']
			publishPom = true
		}
	}
}


